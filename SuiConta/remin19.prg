/*
10-09-2007
ASOCIACION ESPAÑOLA DE BANCA
Adeudos por domiciliaciones en soporte magnético
serie normas y procedimientos bancarios N19
Madrid - Octubre 2005
*/

PROCEDURE REMIN19()
LOCAL PAG:=0,LINR:=0,LINL:=0,TOT1:=0
AbrirDBF("empresa",,,,RUTAREMEMP)
GO RECNOEMPRESA
NORNIFEMP:=IMPNIF(CIF)
NORNOMEMP:=IF(FIELDPOS("NOMEMP")<>0,RTRIM(NOMEMP),RTRIM(EMP))
NORDIREMP:=RTRIM(DIRECCION)
NORPOBEMP:=RTRIM(CODPOS+"-"+POBLACION)

SELEC FIN
GO TOP
CODBAN2:=VAL(RIGHT(STR(CODBAN),4))
EURO2:=1

AbrirDBF("CUENTAS",,,,RUTAREMESA)
SEEK FIN->CODBAN
NORCTAEMP:=CTA_BAN_SUIZO(BANCTA,20)

SELEC FIN
GO TOP
DO WHILE .NOT. EOF()
   SET PRINT ON
   IF LINR=0
*** ANEXO 2.ADEUDOS DOMICILIADOS. PROCEDIMIENTO PRIMERO
*** CABECERA DE PRESENTADOR
      ?? IF(EURO2=0,"01","51") //A1
      ?? "80" //A2
      ?? PADL(NORNIFEMP,9,"0") //B1-1
      ?? PADL(LTRIM(STR(CODBAN2)),3,"0") //B1-2
      ?? DIA(FREM,-6) //B2
      ?? SPACE(6) //B3
      ?? PADR(NORNOMEMP,40," ") //C
      ?? SPACE(20) //D
      ?? PADL(SUBSTR(NORCTAEMP,1,4),4,"0") //E1
      ?? PADL(SUBSTR(NORCTAEMP,5,4),4,"0") //E2
      ?? SPACE(12)
      ?? SPACE(40)
      ?? SPACE(14)
*** CABECERA DE ORDENANTE
      ?  IF(EURO2=0,"03","53") //A1
      ?? "80" //A2
      ?? PADL(NORNIFEMP,9,"0") //B1-1
      ?? PADL(LTRIM(STR(CODBAN2)),3,"0") //B1-2
      ?? DIA(FREM,-6) //B2
      ?? DIA(FVTO,-6) //B3
      ?? PADR(NORNOMEMP,40," ") //C
      ?? PADL(SUBSTR(NORCTAEMP,1,4),4,"0")    //D1
      ?? PADL(SUBSTR(NORCTAEMP,5,4),4,"0")    //D2
      ?? PADL(SUBSTR(NORCTAEMP,9,2),2,"*")    //D3
      ?? PADL(SUBSTR(NORCTAEMP,11,10),10,"0") //D4
      ?? SPACE(8)  //E1
      ?? "01"      //E2
      ?? SPACE(10) //E3
      ?? SPACE(40) //F
      ?? SPACE(14) //G
   ENDIF
*** INDIVIDUAL OBLIGATORIO
   ?  IF(EURO2=0,"06","56") //A1
   ?? "80" //A2
   ?? PADL(NORNIFEMP,9,"0") //B1-1
   ?? PADL(LTRIM(STR(CODBAN2)),3,"0")     //B1-2
   ?? PADR(ALLTRIM(STR(CODCTA)),12," ")   //B2
   ?? PADR(NOMCTA,40," ") //C
   BANCTA2:=CTA_BAN_SUIZO(BANCTA,20)
   ?? PADL(ALLTRIM(SUBSTR(BANCTA2,1,4)),4,"0")    //D1
   ?? PADL(ALLTRIM(SUBSTR(BANCTA2,5,4)),4,"0")    //D2
   ?? PADL(ALLTRIM(SUBSTR(BANCTA2,9,2)),2,"*")    //D3
   ?? PADL(ALLTRIM(SUBSTR(BANCTA2,11,10)),10,"0") //D4
   ?? PADL(LTRIM(FTONUMNOR(IMPORTE,EURO2)),10,"0") //E
   ?? PADR(ALLTRIM(NFRA),6," ")       //F1
   ?? PADR(ALLTRIM(STR(CODCTA)),10," ") //F2

   NSER2:=SERIE
   NREM2:=NREM
   LREM2:=LREM
   AbrirDBF("REMLIN",,,,RUTAREMESA)
   SEEK STR(NSER2,2)+STR(NREM2,5)+STR(LREM2,3)
   IF SERIE=NSER2 .AND. NREM=NREM2 .AND. LREM=LREM2 .AND. .NOT. EOF()
      ?? PADR(LINEA,40," ") //G
   ELSE
      SELEC FIN
      CONCEPTOFRA:="Factura nº "+ALLTRIM(NFRA)
      IF NEMPREG<>0 .AND. NREG<>0
         NFAC2:=NREG
         IF NUMEMP<>NEMPREG
            RUTA1:=BUSRUTAEMP(RUTAPROGRAMA,NUMEMP,NEMPREG,"SUICONTA")
         ELSE
            RUTA1:={RUTAEMPRESA}
         ENDIF
         FOR N=1 TO LEN(RTRIM(NFRA))
            IF ISALPHA(SUBSTR(NFRA,N,1))=.T.
               SFAC2:=SUBSTR(NFRA,N,1)
               EXIT
            ENDIF
         NEXT
         AbrirDBF("FAC92",,,,RUTA1[1],1)
         SEEK SERIE(SFAC2,NFAC2)
         IF CODCTA=FIN->CODCTA .AND. .NOT. EOF()
            CONCEPTOFRA:=CONCEPTOFRA+" Fecha "+DIA(FFAC,10)
         ENDIF
      ENDIF
      ?? PADR(CONCEPTOFRA,40," ") //G
   ENDIF

   SELEC FIN
   ?? SPACE(8) //H
   SET PRINT OFF
   TOT1:=TOT1+IMPORTE
   LINR++

*** INDIVIDUAL OPCIONAL
   AbrirDBF("REMLIN",,,,RUTAREMESA)
   SEEK STR(NSER2,2)+STR(NREM2,5)+STR(LREM2,3)
   IF .NOT. EOF()
      SKIP
   ENDIF
   FOR NN=1 TO 5
      IF SERIE<>NSER2 .OR. NREM<>NREM2 .OR. LREM<>LREM2 .OR. EOF()
         EXIT
      ENDIF
      SET PRINT ON
      SELEC FIN
      ?  IF(EURO2=0,"06","56") //A1
      ?? STR(80+NN,2) //A2
      ?? PADL(NORNIFEMP,9,"0") //B1-1
      ?? PADL(LTRIM(STR(CODBAN2)),3,"0") //B1-2
      ?? PADR(ALLTRIM(STR(CODCTA)),12," ") //B2
      AbrirDBF("REMLIN",,,,RUTAREMESA)
      ?? PADR(LINEA,40," ") //C
      SKIP
      IF SERIE=NSER2 .AND. NREM=NREM2 .AND. LREM=LREM2 .AND. .NOT. EOF()
         ?? PADR(LINEA,40," ") //D
         SKIP
      ELSE
         ?? SPACE(40) //D
      ENDIF
      IF SERIE=NSER2 .AND. NREM=NREM2 .AND. LREM=LREM2 .AND. .NOT. EOF()
         ?? PADR(LINEA,40," ") //E
         SKIP
      ELSE
         ?? SPACE(40) //E
      ENDIF
      ?? SPACE(14) //F
      SET PRINT OFF
      LINL++
   NEXT
   SELEC FIN
   SKIP
ENDDO
*** TOTAL ORDENANTE
SET PRINT ON
?  IF(EURO2=0,"08","58") //A1
?? "80" //A2
?? PADL(NORNIFEMP,9,"0") //B1-1
?? PADL(LTRIM(STR(CODBAN2)),3,"0") //B1-2
?? SPACE(12) //B2
?? SPACE(40) //C
?? SPACE(20) //D
?? PADL(LTRIM(FTONUMNOR(TOT1,EURO2)),10,"0") //E1
?? SPACE(6)  //E2
?? PADL(LTRIM(STR(LINR)),10,"0") //F1
?? PADL(LTRIM(STR(LINR+LINL+2)),10,"0") //F2
?? SPACE(20) //F3
?? SPACE(18) //G
*** TOTAL GENERAL
?  IF(EURO2=0,"09","59") //A1
?? "80" //A2
?? PADL(NORNIFEMP,9,"0") //B1-1
?? PADL(LTRIM(STR(CODBAN2)),3,"0") //B1-2
?? SPACE(12) //B2
?? SPACE(40) //C
?? PADL(LTRIM(STR(1)),4,"0") //D1
?? SPACE(16) //D2
?? PADL(LTRIM(FTONUMNOR(TOT1,EURO2)),10,"0") //E1
?? SPACE(6)  //E2
?? PADL(LTRIM(STR(LINR)),10,"0") //F1
?? PADL(LTRIM(STR(LINR+LINL+4)),10,"0") //F2
?? SPACE(20) //F3
?? SPACE(18) //G
?
SET PRINT OFF
SET PRINTER TO

*
FUNCTION IMPNIF(IMPNIF2)
   IMPNIF3:=""
   FOR N=1 TO LEN(IMPNIF2)
      IF ISDIGIT(SUBSTR(IMPNIF2,N,1)) .OR. ISALPHA(SUBSTR(IMPNIF2,N,1))
         IMPNIF3=IMPNIF3+SUBSTR(IMPNIF2,N,1)
      ENDIF
   NEXT
   RETURN(IMPNIF3)
*
FUNCTION FTONUMNOR(IMP2,EURO2)
   IF EURO2=0
      NOM2:=STR(INT(IMP2))
   ELSE
      NOM2:=STR(IMP2,15,2)
      NOM2:=STRTRAN(NOM2,".","")
   ENDIF
   RETURN(NOM2)

