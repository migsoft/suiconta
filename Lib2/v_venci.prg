FUNCTION VENCI(NIVEL,FPAG,FFACV,IMPAGO,DPAG1,DPAG2,VTO)
LOCAL CPAG,Y1,Y2,Y3,Y4,DESC2,XT1,FVTO2,IMP2,D1,D2,DIF1,DIF2,DIF3
CPAG=STR(FPAG)
CPAG=LTRIM(CPAG)
CPAG=CPAG+"   "
Y1=VAL(SUBSTR(CPAG,1,1)) //FORMA DE PAGO
Y2=VAL(SUBSTR(CPAG,2,1)) //NUMERO DE GIROS
Y3=VAL(SUBSTR(CPAG,3,1)) //DIAS 1§ VTO.
Y4=VAL(SUBSTR(CPAG,4,1)) //DIAS RESTO
DO CASE
CASE NIVEL=1  //FORMA DE PAGO
   DO CASE
      CASE Y1=0
         DESC2="Contado"
      CASE Y1=1
         DESC2="Reposicion"
      CASE Y1=2
         DESC2="Transferencia"
      CASE Y1=3
         DESC2="Giro"
      CASE Y1=4
         DESC2="Talon"
      CASE Y1=5
         DESC2="Pagare"
      CASE Y1=6
         DESC2="Reembolso"
      CASE Y1=7
         DESC2="Contado Leasing"
      CASE Y1=8
         DESC2="Contado Metalico"
      OTHERWISE
         DESC2="ERROR"
   ENDCASE
   XT=(Y4*15)
   IF Y2>0 .AND. Y1>0
      XT1=Y3*15
      DESC2=DESC2+" "+LTRIM(STR(XT1))
      IF Y2>1
         FOR N=2 TO Y2
            XT1=XT1+XT
            DESC2=DESC2+","+LTRIM(STR(XT1))
         NEXT
      ENDIF
      DESC2=DESC2+" dias"
   ENDIF
   RETURN(DESC2)
CASE NIVEL=2   //VENCIMIENTOS
   XT=(Y3*15)
   FVTO2=FFACV+XT
   IF VTO>1
      FVTO2=FVTO2+((VTO-1)*Y4*15)
   ENDIF
   *** AJUSTAR DIA
   IF MAX(DPAG1,DPAG2)>0
      DIF1:=CTOD("01-"+STR(MONTH(FFACV))+"-"+STR(YEAR(FFACV)))
      DIF2:=CTOD("01-"+STR(MONTH(FVTO2))+"-"+STR(YEAR(FVTO2)))
      DIF3:=DIF2-DIF1
      DIF3:=DIF3-(ROUND(DIF3/30,0)*30)
      FVTO2=FVTO2+DIF3
      FVTO2=FVTO2-7
      D1:=MIN(DAY(FFACV),30)
      IF MAX(DPAG1,DPAG2)>28
         D1=IF(D1>28,MAX(DPAG1,DPAG2),D1)
      ENDIF
      D2:=IF(D1>15,D1-15,D1+15)
      FVTO2=MESDIA(FVTO2,D1,D2)
   ENDIF
   *** FIN AJUSTAR DIA
   FVTO2=MESDIA(FVTO2,DPAG1,DPAG2)
   RETURN(FVTO2)
CASE NIVEL=3  //IMPORTES
   IF Y2=0 .OR. VTO>Y2
      IMP2=0
   ELSE
      IMP2=ROUND(IMPAGO/Y2,MDA_DEC(EJERANY))
      IF VTO=Y2
         IMP2=IMPAGO-(ROUND(IMPAGO/Y2,MDA_DEC(EJERANY))*(Y2-1))
      ENDIF
   ENDIF
   RETURN(IMP2)
ENDCASE

*
FUNCTION VENCIR(VTOEMP2,VTOREG2,VTOFREG2)
   LOCAL NUMSEL:=SELECT()

   IF VTOEMP2<>NUMEMP
      RUTA1:=BUSRUTAEMP(RUTAPROGRAMA,VTOEMP2,0,"SUICONTA")
      RUTA2:=RUTA1[1]
   ELSE
      RUTA2:=RUTAEMPRESA
   ENDIF

   NOM2:="Vto."
   IF FILE(RUTA2+"\FACVTO.DBF")=.T.
      AbrirDBF("FACVTO",,,,RUTA2,1)
      SEEK VTOREG2
      DO WHILE NREG=VTOREG2 .AND. NEMP=VTOEMP2 .AND. .NOT. EOF()
         DIAS:=FVTO-VTOFREG2
         NOM2:=NOM2+IF(LEN(NOM2)<=4,"",",")+LTRIM(STR(DIAS))
         SKIP
      ENDDO
      IF LEN(NOM2)<=3
         NOM2:="Contado"
      ELSE
         NOM2:=NOM2+" dias"
      ENDIF
   ENDIF

   SELEC &NUMSEL
   RETURN(NOM2)


*
FUNCTION VENCI_NC(FPAGO,LARGO)
   LARGO:=IF(LARGO=NIL,900,LARGO)
   IF VALTYPE(FPAGO)="N"
      NOMFPAG:=VENCI(1,FPAGO,,,,,) //FACTURAS EMITIDAS
   ELSE
      NOMFPAG:=FPAGO               //FACTURAS RECIBIDAS
   ENDIF
   IF LEN(NOMFPAG)>LARGO
      IF AT("dias",NOMFPAG)<>0
         NOMFPAG:=LEFT(NOMFPAG,AT("dias",NOMFPAG)-2)
      ENDIF
      IF LEN(NOMFPAG)>LARGO
         IF AT(" ",NOMFPAG)>4
            NOMFPAG=LEFT(NOMFPAG,4)+SUBSTR(NOMFPAG,AT(" ",NOMFPAG),LEN(NOMFPAG))
         ENDIF
         IF LEN(NOMFPAG)>LARGO
            IF AT(" ",NOMFPAG)<>0 .OR. AT(".",NOMFPAG)<>0
               NOMFPAG=LEFT(NOMFPAG,LARGO-1)+"+"
            ELSE
               NOMFPAG=LEFT(NOMFPAG,LARGO)
            ENDIF
         ENDIF
      ENDIF
   ENDIF
   NOMFPAG=PADR(NOMFPAG,LARGO," ")
RETURN(NOMFPAG)

